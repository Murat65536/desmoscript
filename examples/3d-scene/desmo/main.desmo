

 

import "../../../desmo-lib/3d.desmo" as t;
import "../../../desmo-lib/binary.desmo" as bin;
import "project.desmo" as project;





settings @{
  viewport: {
    xmin: -2,
    ymin: -2,
    xmax: 2,
    ymax: 2 
  },
  showGrid: false,
  showXAxis: false,
  showYAxis: false
}




cuboidSize = 8;
multiObjPhysicsToDesmoscript!(physicsMeshData, "assets/world.obj")
lookupPhysicsMesh!(physicsMesh, physicsMeshData, 
  -1*cuboidSize + controls.lookupPos.x, 
  -1*cuboidSize + controls.lookupPos.y, 
  -1*cuboidSize + controls.lookupPos.z,
  cuboidSize + controls.lookupPos.x, 
  cuboidSize + controls.lookupPos.y, 
  cuboidSize + controls.lookupPos.z,
  3
)
multiObjCelShadingToDesmoscript!(graphicsMeshData, "assets/world.obj")
lookupCelShadingMesh!(  
  graphicsMesh, graphicsMeshData,
  -1*cuboidSize + controls.lookupPos.x, 
  -1*cuboidSize + controls.lookupPos.y, 
  -1*cuboidSize + controls.lookupPos.z,
  cuboidSize + controls.lookupPos.x, 
  cuboidSize + controls.lookupPos.y, 
  cuboidSize + controls.lookupPos.z,
  3
)




currentFrame = 0;



fn mix(a, b, factor) {
  show clampedFactor = min(max(factor, 0), 1); with @{ hidden: true }
  b * clampedFactor + a * (1 - clampedFactor)
}


sub!(proj, p, (p.x / p.z, p.y / p.z))

ns display {
  // calculate view space
  deswizzle!(translated, graphicsMesh.vertexPosition.xyz - controls.pos.xyz)
  ns rotated1 {
    x = translated.x * cos(controls.rotation\x) - translated.z * sin(controls.rotation\x);
    y = translated.y;
    z = translated.x * sin(controls.rotation\x) + translated.z * cos(controls.rotation\x);
  }
  ns view {  
    x = rotated1.x;     
    y = rotated1.y * cos(controls.rotation\y) - rotated1.z * sin(controls.rotation\y);  
    z = rotated1.y * sin(controls.rotation\y) + rotated1.z * cos(controls.rotation\y);
  }
    
  // get average positions in world and view space
  deswizzle!(average, (   
    graphicsMesh.vertexPosition.xyz[graphicsMesh.index.tri1] 
    + graphicsMesh.vertexPosition.xyz[graphicsMesh.index.tri2]
    + graphicsMesh.vertexPosition.xyz[graphicsMesh.index.tri3]
  ) / 3)
  deswizzle!(viewAverage, average.xyz - controls.pos.xyz)
  t.normalize!(normalizedViewAverage, viewAverage)

  // depth sorting
  polygonDepths = normalizedViewAverage.mag;
  depthSortOrder = sort(1 .. length(graphicsMesh.index.tri1), -1 * polygonDepths);
    
  deswizzle!(view1, view.xyz[graphicsMesh.index.tri1])
  deswizzle!(view2, view.xyz[graphicsMesh.index.tri2])
  deswizzle!(view3, view.xyz[graphicsMesh.index.tri3])

  /*
  show screenSpace1 = (proj!(view1)); with @{ hidden: true }
  show screenSpace2 = (proj!(view2)); with @{ hidden: true }
  show screenSpace3 = (proj!(view3)); with @{ hidden: true }

  sub!(windingOrderContrib, start, end, (end\x - start\x) * (end\y + start\y))

  windingOrders = match {
    (sign(view1.z - 0.05) + sign(view2.z - 0.05) + sign(view3.z - 0.05)) == 3 =>     
      windingOrderContrib!(screenSpace1, screenSpace2)
    + windingOrderContrib!(screenSpace2, screenSpace3)
    + windingOrderContrib!(screenSpace3, screenSpace1);
    1;
  };*/

  deswizzle!(edge1, graphicsMesh.vertexPosition.xyz[graphicsMesh.index.tri2] - graphicsMesh.vertexPosition.xyz[graphicsMesh.index.tri1])
  deswizzle!(edge2, graphicsMesh.vertexPosition.xyz[graphicsMesh.index.tri3] - graphicsMesh.vertexPosition.xyz[graphicsMesh.index.tri1])
  t.cross!(notNormalizedNormal, edge1, edge2)
  backfaceDeterminant = t.dotns!(notNormalizedNormal, normalizedViewAverage);
  
  polygonFilter = depthSortOrder[backfaceDeterminant[depthSortOrder] < 0];


  fn smoothstepMix(a, b, factor) {
    mix(a, b, factor * factor * (3 - factor * 2))
  }

  fn rand(co1, co2) {
    mod(sin(co1 * 12.9898 + co2 * 78.223) * 43758.5453, 1)
  }

  flickeringPeriod = 8;
  glowingPeriod = 60;

  fn randomlyModulateLight(seed, period, brightnessVariation) {
    smoothstepMix(
      rand(seed, floor(currentFrame / period)) * brightnessVariation + 1 - brightnessVariation,
      rand(seed, floor(currentFrame / period) + 1) * brightnessVariation + 1 - brightnessVariation,
      mod(currentFrame, period) / period 
    )
  }

  lightBrightnesses = [
    1, 
    randomlyModulateLight(1, flickeringPeriod, 0.3),
    randomlyModulateLight(2, flickeringPeriod, 0.3), 
    randomlyModulateLight(3, glowingPeriod, 0.8),
    randomlyModulateLight(4, glowingPeriod, 0.8)];

  light1Brightnesses = lightBrightnesses[graphicsMesh.light1Type+1];
  light2Brightnesses = lightBrightnesses[graphicsMesh.light2Type+1];
  colors = rgb( 
    graphicsMeshData.r[graphicsMesh.baseColor]
     + graphicsMeshData.r[graphicsMesh.light1Color] * light1Brightnesses
     + graphicsMeshData.r[graphicsMesh.light2Color] * light2Brightnesses, 
    graphicsMeshData.g[graphicsMesh.baseColor]
     + graphicsMeshData.g[graphicsMesh.light1Color] * light1Brightnesses
     + graphicsMeshData.g[graphicsMesh.light2Color] * light2Brightnesses,
    graphicsMeshData.b[graphicsMesh.baseColor]
     + graphicsMeshData.b[graphicsMesh.light1Color] * light1Brightnesses
     + graphicsMeshData.b[graphicsMesh.light2Color] * light2Brightnesses
  );  

  colors2 = colors;

  // display polygons
  show polys = [
  project.getClippedPolygon(
    0.01,
    view1.x[i], view1.y[i], view1.z[i],
    view2.x[i], view2.y[i], view2.z[i],
    view3.x[i], view3.y[i], view3.z[i]
    )
  for
  i=1 .. length(graphicsMesh.index.tri1)
  ][polygonFilter]; with @{
    color: "red",
    lines: false,
    fillOpacity: ds(1),
    colorLatex: ds(
        colors[polygonFilter]
    )
  }
}



fn isPointInsideAABB(px, py, pz, xmin, ymin, zmin, xmax, ymax, zmax) {
  match {
    px < xmin => 0;
    py < ymin => 0;
    pz < zmin => 0;
    px > xmax => 0;
    py > ymax => 0;
    pz > zmax => 0;
    1;
  } 
}

fn isOverlapping(amin, amax, bmin, bmax) {
  min(
    match { (amax >= bmin) => 1; 0; },
    match { (bmax >= amin) => 1; 0; }
  )
}

fn isTriPossiblyInsideAABB(
  x1, y1, z1,
  x2, y2, z2,
  x3, y3, z3,
  xmin, ymin, zmin,
  xmax, ymax, zmax
) {
  xtrimin = min(x1, x2, x3);
  ytrimin = min(y1, y2, y3);
  ztrimin = min(z1, z2, z3);
  xtrimax = max(x1, x2, x3);
  ytrimax = max(y1, y2, y3);
  ztrimax = max(z1, z2, z3);
  min(
    isOverlapping(xmin, xmax, xtrimin, xtrimax),
    isOverlapping(ymin, ymax, ytrimin, ytrimax),
    isOverlapping(zmin, zmax, ztrimin, ztrimax)
  )
}




ns physics {
  deswizzle!(allpos1, physicsMesh.vertexPosition.xyz[physicsMesh.index.tri1])
  deswizzle!(allpos2, physicsMesh.vertexPosition.xyz[physicsMesh.index.tri2])
  deswizzle!(allpos3, physicsMesh.vertexPosition.xyz[physicsMesh.index.tri3])
  filter = (1 .. length(allpos1.x))[
    isTriPossiblyInsideAABB(
      allpos1.x, allpos1.y, allpos1.z,
      allpos2.x, allpos2.y, allpos2.z,
      allpos3.x, allpos3.y, allpos3.z,
      controls.physicsPos.x - controls.velMag,
      controls.physicsPos.y - controls.velMag,
      controls.physicsPos.z - controls.velMag,
      controls.physicsPos.x + controls.velMag,
      controls.physicsPos.y + controls.velMag,
      controls.physicsPos.z + controls.velMag
    ) == 1   
  ];

  deswizzle!(pos1, allpos1.xyz[filter])
  deswizzle!(pos2, allpos2.xyz[filter])
  deswizzle!(pos3, allpos3.xyz[filter])

  posIndices = match {
    length(pos1.x) == 0 => [];
    (1 .. length(pos1.x));
  };

  deswizzle!(edge1, allpos2.xyz - allpos1.xyz) 
  deswizzle!(edge2, allpos3.xyz - allpos1.xyz)
  t.cross!(notNormalizedNormal, edge1, edge2)
  t.normalize!(allnormal, notNormalizedNormal)

  deswizzle!(normal, allnormal.xyz[filter])

  fn noUndefined(x, alternative) {
    match {
      (x == x) => x;
      alternative;
    }
  }
  
  loop!(physicsLoop, 2, {
    t.vec3!(rayPos, controls.physicsPos.x, controls.physicsPos.y, controls.physicsPos.z)
    t.normalize!(rayDir, controls.velocity)
    distanceRemaining = rayDir.mag;
    isOnGround = 0;
  }, {
    rayTriangleIntersections =
      t.rayTriangleIntersectionWithIndex( 
        prev.rayPos.x, prev.rayPos.y, prev.rayPos.z,
        prev.rayDir.x, prev.rayDir.y, prev.rayDir.z,
        pos1.x, pos1.y, pos1.z,
        pos2.x, pos2.y, pos2.z,
        pos3.x, pos3.y, pos3.z,
        posIndices
      );

    aboveZeroRTIs = rayTriangleIntersections[rayTriangleIntersections\x > 0];

    closestIntersect = aboveZeroRTIs[aboveZeroRTIs\x == min(aboveZeroRTIs\x)][1];

    anyIntersects = match { (closestIntersect\x == closestIntersect\x) => 1; 0; };

    distanceToIntersection = match {
      anyIntersects == 0 => prev.distanceRemaining;
      min(closestIntersect\x, prev.distanceRemaining);
    };

    distanceRemaining = prev.distanceRemaining - distanceToIntersection;

    t.vec3!(rayPos,
      prev.rayPos.x + prev.rayDir.x * (distanceToIntersection - 0.0001),
      prev.rayPos.y + prev.rayDir.y * (distanceToIntersection - 0.0001),
      prev.rayPos.z + prev.rayDir.z * (distanceToIntersection - 0.0001)
    )

    indexOfIntersection = closestIntersect\y;

    t.vec3!(intersectedTriNormal, 
      normal.x[indexOfIntersection],
      normal.y[indexOfIntersection],
      normal.z[indexOfIntersection]
    )

    idotn = t.dotns!(prev.rayDir, intersectedTriNormal);

    isOnGround = max(
      prev.isOnGround,
      match { intersectedTriNormal.y > 0 => 1; 0; }
    );

    ns rayDir {
      x = match{(distanceRemaining == 0) => prev.rayDir.x;
        prev.rayDir.x - 2 * idotn * intersectedTriNormal.x;};
      y = match{(distanceRemaining == 0) => prev.rayDir.y;
        prev.rayDir.y - 2 * idotn * intersectedTriNormal.y;};
      z = match{(distanceRemaining == 0) => prev.rayDir.z;
        prev.rayDir.z - 2 * idotn * intersectedTriNormal.z;};
    }
  })
}







 

ticker @{ 
  handlerLatex: ds( 
    & 
    match { (mod(currentFrame, 30) == 0) => controls.updateMeshLookup; },
    currentFrame -> currentFrame + 1,
    match { mod(currentFrame, controls.physicsInterval) == 0 => controls.doPhysics; } ,
    controls.doPerFramePhysics
  ),  
  playing: true, 
  open: true
}  

 
ns controls { 
  t.vec3!(physicsPos, 0, 1, 0)

  physicsInterval = 6;
  
  velFactor = mod(currentFrame - 1, physicsInterval) / physicsInterval;
  
  t.vec3!(pos, 
    mix(physicsPos.x, physics.physicsLoop.rayPos.x, velFactor), 
    mix(physicsPos.y, physics.physicsLoop.rayPos.y, velFactor) 
    + 1.5 + 0.1 * min(0, 1 - framesSinceOnGround / 8) * sin(accumDist * 0.4), 

    mix(physicsPos.z, physics.physicsLoop.rayPos.z, velFactor)
  )
  t.vec3!(velocity, 0.001, 0.0, 0.001) 
  t.vec3!(lookupPos, 0, 0, 0) 

  updateMeshLookup = &
    lookupPos.x -> pos.x,
    lookupPos.y -> pos.y,
    lookupPos.z -> pos.z;

  accumDist = 0;

  velMag = (velocity.x ^ 2 + velocity.y ^ 2 + velocity.z ^ 2) ^ 0.5;

  doPhysics = &
    physicsPos.x -> physics.physicsLoop.rayPos.x,
    physicsPos.y -> physics.physicsLoop.rayPos.y,
    physicsPos.z -> physics.physicsLoop.rayPos.z,
    velocity.x -> physics.physicsLoop.rayDir.x * velMag * 0.6 - 0 + 0.3 * rotationController\y * sin(rotation\x),
    velocity.y -> physics.physicsLoop.rayDir.y * velMag * 0.9 - 0.03 + physics.physicsLoop.isOnGround * 0.031, 
    velocity.z -> physics.physicsLoop.rayDir.z * velMag * 0.6 - 0 + 0.3 * rotationController\y * cos(rotation\x),
    framesSinceOnGround -> match { physics.physicsLoop.isOnGround == 0 => framesSinceOnGround + 1; 0; }
    ;

  framesSinceOnGround = 0;

  doPerFramePhysics = &
    rotation -> (rotation\x + rotationController\x * 0.1, 0),
    accumDist -> accumDist + (velocity.x ^ 2 + velocity.z ^ 2) ^ 0.5
    ;

  joystickOffset = (0, -0.5);

  show rotationView = (0, -0.5); with @{}
  rotationController = rotationView - joystickOffset;
  show rotationJoystick = polygon(joystickOffset, rotationView); with @{}
 
  rotation = (3.14, 0);
}
